The previous chapter discusses using subtyping to define quotient-like types in Coq. This chapter lists examples of quotients that can be easily defined in this way. It focuses on defining quotients with an underlying type living in \mcoq{HSet}. Therefore, we are able to use the classical equality definition and work in \mcoq{Prop} sort.
\section{Unordered pairs}
\begin{defi}{Unordered pairs}{def:upair}
In mathematics, an \emph{unordered pair} is a set of the form $\{a, b\}$, where $\{a, b\} = \{b, a\}$ \cite{SetTheorey}. In contrast to the ordered pair where $(a, b) \neq (b, a)$.
\end{defi}
As mentioned in the introduction, there is no normalization function for unordered pairs in the most general case. Therefore, we will only discuss the case when the underlying type has a full decidable order (defined in \ref{def:fullOrd}). Antisymmetry is required to prove the uniqueness of representations and universality (fullness) is required to define a computable function to construct an unordered pair out of two elements.
\begin{defi}{Full decidable order}{def:fullOrd}
\begin{minted}{coq}
Class FullOrd (A : Type) := {
  ord  : A -> A -> bool;
  asym : forall x y : A, ord x y = true -> x = y;
  full : forall x y : A, ord x y = true \/ ord y x = true;
}.
\end{minted}
\end{defi}
\begin{defi}{Unordered pairs in Coq}{def:coqupair}
\emph{Unordered pairs} can be defined as a triple of first and second element and a proof that states they are in the correct order.
\begin{minted}{coq}
Record UPair (A : Type) `{FullOrd A} := {
  fst    : A;
  snd    : A;
  sorted : ord fst snd = true;
}.
\end{minted}
\end{defi}
In order to use such defined unordered pairs, we need to define a constructor.
\begin{func}{Constructor of unordered pair}{fun:make_UPair}
\begin{minted}{coq}
Definition make_UPait {A : Type} `{FullOrd A} (x y : A) : UPair A.
Proof.
  destruct (ord x y) eqn:o. 
  - econstructor. apply o.
  - assert (o1: ord y x = true) by
    (destruct (full x y); [rewrite H0 in o; discriminate | auto]).
    econstructor. apply o1.
Qed.
\end{minted}
\end{func}

\subsection{Uniquness of representations}
To talk about the uniqueness of representations, firstly we need to define the equivalence relation defining this quotient construction. In the case of unordered pairs, if two pairs contain the same elements, they are in a quotient-defining relation.
\begin{defi}{Equivalence relation for unordered pairs}{def:equiv_upair}
\begin{minted}{coq}
Definition contains {A : Type} `{FullOrd A}  (x y : A)
  (p : UPair A) := (fst p = x /\ snd p = y) \/ 
    (fst p = y /\ snd p = x).

Definition UPair_equiv {A : Type} `{FullOrd A} (p q : UPair A) :=
  forall x y : A, contains x y p <-> contains x y q. 
\end{minted}
\end{defi}
Having a formal definition of equivalence relation, we can formally verify the claim of unique representations for such defined unordered pairs.
\begin{theo}{Uniquness of representation for unordered pairs}{th:uniq_upair}
\begin{minted}{coq}
Theorem UPair_uniq (A : Type) `{FullOrd A} (p q : UPair A) 
  (x y : A) : contains x y p -> contains x y q -> p = q.
\end{minted}
\end{theo}
\begin{proof}{}{proof:uniq_upair}
\begin{minted}{coq}
intros [(cp1 & cp2) | (cp1 & cp2)] [(cq1 & cq2) | (cq1 & cq2)];
destruct p, q; cbn in *; subst.
2-3: assert (x = y) by (apply asym; assumption); subst.
1-4: f_equal; apply bool_is_hset. Qed.
\end{minted}
As we see, the last part of this proof uses \mcoq{bool_is_hset} theorem. As we know, \mcoq{bool} has decidable equality. According to Hedberg's theorem \ref{th:hedberg}, we know it also has unique identity proofs. Formal proof can be found in \coqsource{Lib/HoTT.v}.
\end{proof}
\section{Finite multisets}
\begin{defi}{Multiset}{def:mset}
In mathematics, a \emph{multiset} (also known as \emph{bag} or \emph{mset}) is a modification of a set concept that allows for multiple instances of the same element \cite{SetTheorey}.
\end{defi}
A multiset is another example of a quotient type with applications in everyday programming \cite{MSetApplic}. It is a data structure known from mathematics where the order of elements does not matter. It can be considered as unordered list. Similar to unordered pairs, there is no normalization function for multisets in the most general case. Therefore, in this thesis we will discuss only multisets with underlying types with decidable linear order.
\begin{defi}{Decidable linear order}{def:lo}
\begin{minted}{coq}
Class LinearOrder {A : Type} := {
  ord      : A -> A -> bool;
  anti_sym : forall x y : A, ord x y = true -> ord y x = true ->
               x = y;
  trans    : forall x y z : A, ord x y = true -> 
               ord y z = true -> ord x z = true;
  full     : forall x y : A, ord x y = true \/ ord y x = true;
}.
\end{minted}
\end{defi}
\subsection{The equivalence relation}
Two multisets are in equivalence relation when they contain the same elements in the same quantity. In other words, when they both are permutations of the same list of elements. We propose using an alternative permutation definition. 
\begin{defi}{Permutaion}{def:perm}
\begin{minted}{coq}
Fixpoint count {A : Type} (p : A -> bool) (l : list A) : nat :=
  match l with
  | nil      => O
  | cons h t => if p h then S (count p t) else count p t
  end.

Definition permutation {A : Type} (a b : list A) :=
  forall p : A -> bool, count p a = count p b.
\end{minted}
\end{defi}
This definition works for types with decidable equality, and as we know, decidable linear order implies decidable equality. For such types, the special case of a decidable predicate is the predicate being equal to a specific element. This definition does not require defining all laws of permutations. Therefore, it is easier to use in the context of a sorting function. Proof of the fact that this definition is equivalent to the classic permutation definition for types with decidable equality can be found in \coqsource{Extras/Permutations.v}.
\subsection{The normalization function}
We can use the sorting function as a normalization function for a multiset quotient type. One of the sorting functions with the best asymptotic complexity is merge sort. Moreover, it has a nice functional definition, making it the ideal candidate for a multiset normalization function. We propose a definition using binary trees.
\begin{defi}{Binary tree}{def:bttree}
\begin{minted}{coq}
Inductive BT (A : Type) : Type :=
| leaf : A -> BT A
| node : BT A -> BT A -> BT A.
\end{minted}
\end{defi}
We also define the insert function that keeps the tree balanced out and the function that converts a list into a balanced-out tree.
\begin{func}{Conversion to balanced-out tree}{fn:BTins}
\begin{minted}{coq}
Fixpoint BTInsert {A : Type} (x : A) (tree : BT A) :=
  match tree with
  | leaf y   => node (leaf x)(leaf y)
  | node l r => node r (BTInsert x l)
  end.

Fixpoint listToBT {A : Type} (x : A) (ls : list A) : BT A :=
  match ls with
  | []       => leaf x
  | l :: ls' => BTInsert x (listToBT l ls')
  end.
\end{minted}
\end{func}
As expected, we also need a merging function for two sorted lists.
\begin{func}{Merge}{fn:merge}
\begin{minted}{coq}
Fixpoint merge {A : Type} (ord : A -> A -> bool) (l1 : list A) 
  : (list A) -> list A :=
  match l1 with
  | []       => fun (l2 : list A) => l2
  | h1 :: t1 => fix anc (l2 : list A) : list A :=
    match l2 with
    | []       => l1
    | h2 :: t2 => if ord h1 h2 
                  then h1 :: (merge ord t1) l2
                  else h2 :: anc t2
    end
  end.
\end{minted}
\end{func}
Now we can define the whole merge sort function, using the defined above ancillary functions.
\begin{func}{Merge sort}{fn:mergeSort}
\begin{minted}{coq}
Fixpoint BTSort {A : Type} (ord : A -> A -> bool) 
  (t : BT A) : list A :=
  match t with
  | leaf x   => [x]
  | node l r => merge ord (BTSort ord l) (BTSort ord r)
  end. 

Definition mergeSort {A : Type} `{LinearOrder A}
  (l : list A) : list A :=
  match l with
  | []      => []
  | x :: l' => BTSort ord (listToBT x l')
  end.
\end{minted}
\end{func}
\subsection{Uniqueness of representations}
\begin{theo}{Uniquness of representation for multisets}{th:uniq_mset}
For every multiset, there is exactly one element in \mcoq{quotient mergeSort} type (defined in \ref{def:subtype_quot}). 
\end{theo}
\begin{proof}{}{proof:uniq_mset}
A sorting function should satisfy two requirements:
\setlist{nolistsep}
\begin{itemize}
    \itemsep 0em 
    \item the output is a sorted list,
    \item the output is a permutation of input.
\end{itemize}
Proofs that the sorting function satisfies the above requirements can be found in \coqsource{Lib/MergeSort.v}.\\
Moreover, we can show that for every list of elements, only one permutation of this list is sorted. Proof of this fact is in \coqsource{Lib/Sorted.v}.\\
Combining those facts, we know that sorting is an idempotent function, and there is only one output of this function for each equivalence class. More formal proof can be found in \coqsource{FiniteMultiSet.v}. \qed
\end{proof}
\section{Finite sets}
Sets are a basic construction known from mathematics \cite{SetTheorey}. It is important to mention that a particular element may appear in a set at most once. Therefore, adding an element to a set that already contains this element does not change the result. Moreover,  the order of elements in sets does not matter. Sets can be considered as sorted and deduplicated lists. Like the two previous examples, sets do not have a normalization function in the most general case. Therefore, we will discuss only sets with underlying types with decidable linear order (defined in \ref{def:lo}).
\subsection{The equivalence relation}
Two sets are in equivalence relation when they contain the same elements. In other words, for every element in underlying type, if it is in the first set, it has at least one copy in the second set. Here we also propose an alternative definition of this relation similar to the previous definition of permutations (defined in \ref{def:perm}).
\begin{defi}{Set equivalence}{def:elem_eq}
\begin{minted}{coq}
Fixpoint any {A : Type} (p : A -> bool) (l : list A) : bool :=
  match l with
  | []        => false
  | (x :: l') => if p x then true else any p l'
  end.

Definition Elem_eq {A : Type} (l l' : list A) : Prop := 
  forall p : A -> bool, any p l = any p l'.
\end{minted}
\end{defi}
For types with decidable equality, the definition above is equivalent to the classical definition. Proof of this fact can be found in \coqsource{Lib/Deduplicated}.
\subsection{The normalization function}
For sets, the normalization function needs to remove duplicates and sort elements. A common approach for such a computer science problem is binary tree \cite{knuth}. This paper also uses it to define a normalization function for a set.
\begin{defi}{Binary tree}{def:binTree}
\begin{minted}{coq}
Inductive tree (A : Type) : Type :=
| leaf : tree A
| node : A -> tree A -> tree A -> tree A.
\end{minted}
\end{defi}
In order to use the insert function, we need to use a more convenient interface for comparing elements.
\begin{func}{}{fn:comp}
\begin{minted}{coq}
Definition comp {A : Type} `{LinearOrder A} (x y : A) : comparison 
  := if ord x y then (if ord y x then Eq else Gt) else Lt.
\end{minted}
\end{func}
We can define a function that inserts elements into the sorted binary tree if and only if the tree does not already contain them.
\begin{func}[D]{Insertion to binary tree}{fn:binTreeAdd}
\begin{minted}{coq}
Fixpoint insert_tree {A : Type} `{LinearOrder A} (x : A) 
  (t : tree A) : tree A :=
  match t with
  | leaf       => node x leaf leaf
  | node v l r => match comp x v with
                  | Lt => node v (insert_tree x l) r
                  | Eq => node v l r
                  | Gt => node v l (insert_tree x r)
                  end
  end.
\end{minted}
\end{func}
By converting a list into a sorted and deduplicated tree and back to a list, we get a sorted and deduplicated list.
\begin{func}{Deduplicating sort}{fn:DSort}
\begin{minted}{coq}
Fixpoint to_tree {A : Type} `{LinearOrder A} (l : list A) 
  : tree A := 
  match l with
  | []      => leaf
  | x :: l' => insert_tree x (to_tree l')
  end.

Fixpoint to_list {A : Type} (l : tree A) : list A := 
  match l with
  | leaf       => []
  | node x l r => to_list l ++ [x] ++ to_list r
  end.

Definition DSort {A : Type} `{LinearOrder A} (l : list A) 
  : list A := to_list (to_tree l).
\end{minted}
\end{func}
\subsection{Uniqueness of representations}
\begin{theo}{Uniqueness of representations for sets}{th:uniq_set}
For every set, there is exactly one element in \mcoq{quotient DSort} type (defined in \ref{def:subtype_quot}). 
\end{theo}
\begin{proof}{}{proof:uniq_set}
A sorting and deduplicating function should satisfy the following requirements:
\setlist{nolistsep}
\begin{itemize}
    \itemsep 0em 
    \item the output is a deduplicated list,
    \item the output is a sorted list,
    \item the output and input contain the same elements.
\end{itemize}
Proofs that the function above meets those requirements can be found in \coqsource{Lib/DedupSort.v}.\\
In addition, we know that each set has exactly one sorted and deduplicated list. Proof of this is in  \coqsource{Lib/Deduplicated.v}.\\
On the basis of those facts we can conclude that the normalization function of a set is idempotent. There is exactly one element for each set defining equivalence class in the codomain of this function. Formal proof can be found in \coqsource{FiniteSet.v}. \qed
\end{proof}